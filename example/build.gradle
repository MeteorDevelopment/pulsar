plugins {
    id "java"
    id "application"
}

group "meteordevelopment"
version "0.1.0"
mainClassName = group + ".example.Main"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":pulsar")

    implementation "com.udojava:EvalEx:2.7"

    implementation platform("org.lwjgl:lwjgl-bom:3.3.0")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.joml:joml:1.10.2"

    [ "natives-windows", "natives-linux", "natives-macos" ].forEach {
        runtimeOnly "org.lwjgl:lwjgl::$it"
        runtimeOnly "org.lwjgl:lwjgl-glfw::$it"
        runtimeOnly "org.lwjgl:lwjgl-opengl::$it"
        runtimeOnly "org.lwjgl:lwjgl-stb::$it"
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    manifest {
        attributes "Main-Class": mainClassName
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
