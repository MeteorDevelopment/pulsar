plugins {
    id "java"
    id "maven-publish"
    id "antlr"
}

group "meteordevelopment"
version "0.1.0"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.11.1"

    compileOnly platform("org.lwjgl:lwjgl-bom:3.3.1")

    compileOnly "org.lwjgl:lwjgl"
    compileOnly "org.lwjgl:lwjgl-glfw"
    compileOnly "org.lwjgl:lwjgl-opengl"
    compileOnly "org.lwjgl:lwjgl-stb"
}

sourceSets {
    generated {
        java.srcDir "${projectDir}/src/generated"
    }

    main {
        java.srcDirs += "${projectDir}/src/generated/java/"
    }
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-package", "meteordevelopment.pulsar", "-visitor"]
    outputDirectory = file("${projectDir}/src/generated/java/meteordevelopment/pulsar")
}

compileJava.dependsOn generateGrammarSource
//sourcesJar.dependsOn generateGrammarSource

clean {
    delete file("${projectDir}/src/generated")
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    publications {
        java(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "meteor-maven"
            url = "https://maven.meteordev.org"

            credentials {
                username = System.getenv("MAVEN_METEOR_ALIAS")
                password = System.getenv("MAVEN_METEOR_TOKEN")
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}
