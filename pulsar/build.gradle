plugins {
    id "java"
    id "java-library"
    id "maven-publish"
}

group "meteordevelopment"
version "0.1.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":pts")

    compileOnly platform("org.lwjgl:lwjgl-bom:3.3.1")

    compileOnly "org.lwjgl:lwjgl"
    compileOnly "org.lwjgl:lwjgl-glfw"
    compileOnly "org.lwjgl:lwjgl-opengl"
    compileOnly "org.lwjgl:lwjgl-stb"

    annotationProcessor "com.github.bsideup.jabel:jabel-javac-plugin:0.4.2"
    compileOnly "com.github.bsideup.jabel:jabel-javac-plugin:0.4.2"
}

configure([tasks.compileJava]) {
    sourceCompatibility = 16
    options.release = 8

    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    publications {
        java(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "meteor-maven"
            url = "https://maven.meteordev.org"

            credentials {
                username = System.getenv("MAVEN_METEOR_ALIAS")
                password = System.getenv("MAVEN_METEOR_TOKEN")
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}
